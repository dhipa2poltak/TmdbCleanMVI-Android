plugins {
  id 'com.android.application'
  id 'org.jetbrains.kotlin.android'
  id 'kotlin-kapt'
}

android {
  compileSdk 33

  defaultConfig {
    applicationId "com.dpfht.tmdbcleanmvi"
    minSdk 16
    targetSdk 33
    versionCode 1
    versionName "1.0"

    multiDexEnabled true

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }

  buildTypes {
    release {
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

      manifestPlaceholders.appNameSuffix = ""
    }
    debug {
      applicationIdSuffix ".debug"
      versionNameSuffix "-debug"
      manifestPlaceholders.appNameSuffix = " (debug)"
    }
  }
  buildFeatures {
    viewBinding true
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  kotlinOptions {
    jvmTarget = '1.8'
  }

  flavorDimensions "default"

  productFlavors {
    staging {
      applicationId "com.dpfht.tmdbcleanmvi.staging"

      manifestPlaceholders.appName = "TMDB Clean MVI-Staging"
      buildConfigField 'String', 'BASE_URL', STAGING_BASE_URL
      buildConfigField 'String', 'IMAGE_URL_BASE_PATH', STAGING_IMAGE_URL_BASE_PATH
    }
    verify {
      applicationId "com.dpfht.tmdbcleanmvi.verify"

      manifestPlaceholders.appName = "TMDB Clean MVI-Verify"
      buildConfigField 'String', 'BASE_URL', VERIFY_BASE_URL
      buildConfigField 'String', 'IMAGE_URL_BASE_PATH', VERIFY_IMAGE_URL_BASE_PATH
    }
    prod {
      applicationId "com.dpfht.tmdbcleanmvi"

      manifestPlaceholders.appName = "TMDB Clean MVI"
      buildConfigField 'String', 'BASE_URL', PROD_BASE_URL
      buildConfigField 'String', 'IMAGE_URL_BASE_PATH', PROD_IMAGE_URL_BASE_PATH
    }
  }
}

dependencies {

  def appDependencies = rootProject.ext.appDependencies

  implementation fileTree(dir: "libs", include: ["*.jar"])
  implementation project(':domain')
  implementation project(':data')
  implementation project(':framework')
  implementation project(":features:feature_splash")
  implementation project(":features:feature_genre")
  implementation project(":features:feature_movies_by_genre")
  implementation project(":features:feature_movie_details")
  implementation project(":features:feature_movie_reviews")
  implementation project(":features:feature_movie_trailer")
  implementation project(":features:feature_error_message")

  implementation 'androidx.core:core-ktx:1.9.0'
  implementation 'androidx.appcompat:appcompat:1.6.1'
  implementation 'com.google.android.material:material:1.8.0'
  implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
  testImplementation 'junit:junit:4.13.2'
  androidTestImplementation 'androidx.test.ext:junit:1.1.5'
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

  implementation appDependencies.navigationFragment
  implementation appDependencies.navigationUI

  implementation appDependencies.toothpickKtp
  kapt appDependencies.toothpickCompiler

  implementation appDependencies.multidex
}
